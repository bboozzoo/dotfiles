# -*- mode: sh -*-

TMPPREFIX=/tmp/
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
# emacs key bindings
bindkey -e
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line

setopt auto_pushd \
    noclobber \
    share_history \
    extended_history \
    complete_aliases \
    extended_glob \
    list_types \
    no_list_beep \
    no_menu_complete \
    prompt_subst

# line editing
autoload -U select-word-style
select-word-style bash

# completion
autoload -Uz compinit
compinit
zstyle ':completion:*' list-colors "$LS_COLORS"
zstyle ':completion:*' completer _complete _correct _approximate
zstyle ':completion:*:correct:::' max-errors 2
zstyle ':completion:*:approximate:::' max-errors 2

# VCS support
autoload -Uz vcs_info
zstyle ':vcs_info:*' actionformats \
    '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
zstyle ':vcs_info:*' formats       \
    '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{5}]%f '
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'
zstyle ':vcs_info:*' enable git cvs svn

# prompt
autoload -U promptinit
promptinit
PROMPT='[%n@%m:%3~]${vcs_info_msg_0_}$ '


# set terminal tile
function title() {
    # escape '%' chars in $1, make nonprintables visible
    local a=${(V)1//\%/\%\%}

    # Truncate command, and join lines.
    a=$(print -Pn "%40>...>$a" | tr -d "\n")

    # additional user specified title
    [[ -n "$TITLE" ]] && title="$TITLE - "

    case $TERM in
        screen*)
            print -Pn "\e]2;$title$a @ $2\a" # plain xterm title
            #print -Pn "\ek$a\e\\"      # screen title (in ^A")
            print -Pn "\e_$2   \e\\"   # screen location
            ;;
        xterm*|rxvt*)
            print -Pn "\e]2;$title$a @ $2\a" # plain xterm title
            ;;
    esac
}

precmd_title() {
    title "zsh" "%m: %3~"
    # vcs_info
}

precmd_vcs() {
    vcs_info
}

preexec_title() {
    title "$1" "%m: %3~"
}

# workaround precmd/preexec redefined by mc
precmd_functions=(precmd_title precmd_vcs)
preexec_functions=(preexec_title)

# always enable 256 colors when proper config is found
if [[ "$TERM" == "xterm" && \
    -e /usr/share/terminfo/x/xterm-256color ]]; then
    TERM=xterm-256color
# elif [[ -n "$TMUX" ]]; then
#     if ! grep default-terminal ~/.tmux.conf >& /dev/null; then
#         [[ "$TERM" != "screen-256color" ]] && TERM=screen-256color
#     fi
# else
#    TERM=gnome-256color
fi

# paths
PATH="$PATH:$HOME/bin:$HOME/.local/bin"

# aliases
alias grep='grep --color=auto'
alias ls='ls --color=auto'
alias ll='ls --color=auto -l'
alias lla='ls --color=auto -la'

if which emacsclient &> /dev/null ; then
    # set aliases
    alias vi="emacsclient -t -a ''"
    alias vim="emacsclient -t -a ''"
    alias rvim=/usr/bin/vim
    # set EDITOR
    EDITOR="emacsclient -t -a ''"
    export EDITOR
fi

# for python
alias pip='pip-python'
alias pylab='ipython --pylab=gtk'
alias ipythongui='ipython qtconsole'

alias dmesg='dmesg --human'
# editors & pagers
PAGER='less'

# mutt
alias mwork='mutt -F ~/.config-custom/mutt/work'
# locale
#LC_ALL=C

BOOKMARKFILE=~/.zsh-bookmarks

# exports
export PATH PAGER TERM LC_ALL

# source zaw if exists
[[ -e ~/code/zaw/zaw.zsh ]] && source ~/code/zaw/zaw.zsh

